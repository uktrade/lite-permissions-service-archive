buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:3.3.7'
  }
}

plugins {
  id 'java'
  id 'application'
  id 'org.flywaydb.flyway' version '4.0.1'
  id 'com.github.johnrengelman.shadow' version '2.0.2'
  id "au.com.dius.pact" version "3.5.13"
  id 'jacoco'
}

apply plugin: 'maven-publish'

group 'uk.gov.bis.lite'

project.version = project.hasProperty('projVersion') ? project.projVersion : '1.0-SNAPSHOT'

mainClassName = "uk.gov.bis.lite.permissions.PermissionsApp"

ext.dropwizardVersion = '1.2.2'

repositories {
  mavenCentral()
  jcenter()
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases/"
  }
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
  }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'org.quartz-scheduler:quartz:2.1.5'
  compile 'org.postgresql:postgresql:42.1.4'
  compile 'org.flywaydb:flyway-core:4.0.1'

  // Lite dependencies
  compile 'uk.gov.bis.lite:json-console-appender:1.1.1'
  compile 'uk.gov.bis.lite:spire-client:1.8'
  compile 'uk.gov.bis.lite:jwt:1.3'
  compile 'uk.gov.bis.lite:lite-customer-service-api:1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'
  compile 'uk.gov.bis.lite:paas-utils:1.0'
  compile 'uk.gov.bis.lite:basic-auth:1.1'
  compile 'uk.gov.bis.lite:redis:0.1.3'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile('com.github.tomakehurst:wiremock:2.7.1') {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  }
  testCompile 'org.skyscreamer:jsonassert:1.3.0'

  testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23') {
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    exclude group: 'junit', module: 'junit'
  }
  testCompile 'org.awaitility:awaitility-groovy:3.0.0'

  testCompile 'ru.yandex.qatools.embed:postgresql-embedded:2.6'
  testCompile 'com.h2database:h2:1.4.196'

  // Pact
  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.5.13'
  testCompile 'au.com.dius:pact-jvm-consumer-junit_2.11:3.5.13'
  testCompile 'org.apache.httpcomponents:fluent-hc:4.5'
}

pact {
  publish {
    pactDirectory = 'target/pacts'
    pactBrokerUrl = 'http://pact-broker.ci.uktrade.io/'
    version = '1.0.3'
  }
}

flyway {
  url = 'jdbc:postgresql://localhost:5432/postgres?currentSchema=permissionsvc'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'config.yaml' }

def nexusUsername = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
def nexusPassword = project.hasProperty('nexusPassword') ? project.nexusPassword : ''

publishing {
  repositories {
    maven {
      name "snapshots"
      url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "releases"
      url "https://nexus.ci.uktrade.io/repository/maven-releases/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/permissions/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/permissions/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-permissions-service-api'
      version '1.4'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }
}
